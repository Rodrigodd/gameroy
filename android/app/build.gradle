import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.android.application'
    id 'org.mozilla.rust-android-gradle.rust-android'
}

android {
    sourceSets {
        main {
            assets.srcDirs layout.buildDirectory.dir("generatedAssets")
        }
    }

    compileSdk 30

    ndkVersion "24.0.8215888"

    defaultConfig {
        applicationId "io.github.rodrigodd.gameroy"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// only sign apks if there is a keystore properties files.
def keyStorePropertiesFile = rootProject.file("keystore.properties")
if (keyStorePropertiesFile.exists()) {
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keyStorePropertiesFile))

    android {
        signingConfigs {
            release {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
        buildTypes {
            debug.signingConfig signingConfigs.release
            release.signingConfig signingConfigs.release
        }
    }
}

if (false) {
    android.packagingOptions.jniLibs.keepDebugSymbols += "**/arm64-v8a/libgameroy_android.so"
}

cargo {
    module = ".."
    libname = "gameroy_android"
    targets = [
        'arm',
        'arm64',
        'x86',
        'x86_64'
    ]
    targetDirectory = "../../target"
}

tasks.whenTaskAdded { task ->
    if (task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders') {
        task.dependsOn 'cargoBuild'
    }
    if (task.name == 'generateDebugAssets' || task.name == 'generateReleaseAssets') {
        task.dependsOn 'generateLicenseAsset'
    }
    if (task.name == 'lintVitalAnalyzeRelease') {
        task.dependsOn 'generateLicenseAsset'
    }
    for (target in cargo.targets) {
        if (task.name == "cargoBuild${target.capitalize()}") {
            task.dependsOn "copy_libc++_shared${target.capitalize()}"
        }
    }
}

for (target in cargo.targets) {
    tasks.register("copy_libc++_shared${target.capitalize()}", Copy) {
        def ndkDir = android.ndkDirectory
        // hostTag, abi and archTriple from: https://developer.android.com/ndk/guides/other_build_systems

        def hostTag
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            if (Os.isArch("x86_64") || Os.isArch("amd64")) {
                hostTag = "windows-x86_64"
            } else {
                hostTag = "windows"
            }
        } else if (Os.isFamily(Os.FAMILY_MAC)) {
            hostTag = "darwin-x86_64"
        } else {
            hostTag = "linux-x86_64"
        }

        def (abi, archTriple) = [
            arm: ['armeabi-v7a', 'arm-linux-androideabi'],
            arm64: ['arm64-v8a', 'aarch64-linux-android'],
            x86: ['x86', 'i686-linux-android'],
            x86_64: ['x86_64', 'x86_64-linux-android'],
        ][target]

        def from_path = "$ndkDir/toolchains/llvm/prebuilt/$hostTag/sysroot/usr/lib/$archTriple/libc++_shared.so"
        def into_path = layout.buildDirectory.dir("rustJniLibs/android/$abi")

        assert file(from_path).exists()

        from from_path
        into into_path
    }
}

tasks.register('generateLicenseAsset', Exec) {
    def cargo = "$rootDir/Cargo.toml"
    def about = "$rootDir/../license/about.toml"
    def hbs = "$rootDir/../license/about.hbs"

    def output = "$buildDir/generatedAssets/license.html"

    inputs.files(cargo, about, hbs)
    outputs.file(output)
    
    commandLine 'cargo', 'about', 'generate', 
        '-m', cargo, 
        '-c', about,
        hbs,
        '-o', output
}

task runDebug(type: Exec, dependsOn: ':app:installDebug') {
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'io.github.rodrigodd.gameroy/io.github.rodrigodd.gameroy.MainActivity'
}

dependencies {

}
