plugins {
    id 'com.android.application'
    id 'org.mozilla.rust-android-gradle.rust-android'
}

android {
    compileSdk 30

    ndkVersion "24.0.8215888"

    defaultConfig {
        applicationId "io.github.rodrigodd.gameroy"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

cargo {
    module = ".."
    libname = "gameroy_android"
    targets = ["arm64"]
    targetDirectory = "../../target"
}

tasks.whenTaskAdded { task ->
    if (task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders') {
        task.dependsOn 'cargoBuild'
    }
    if (task.name == 'cargoBuildArm64') {
        task.dependsOn 'copy_libc++_sharedArm64'
    }
}

// TODO: make a task for each architecture
// TODO: detect the correct host tag
tasks.register('copy_libc++_sharedArm64', Copy) {
    def ndkDir = android.ndkDirectory
    // hostTag and archTriple from: https://developer.android.com/ndk/guides/other_build_systems
    def hostTag = 'windows-x86_64'
    def archTriple = 'aarch64-linux-android'
    from "$ndkDir/toolchains/llvm/prebuilt/$hostTag/sysroot/usr/lib/$archTriple/libc++_shared.so"
    into layout.buildDirectory.dir("rustJniLibs/android/arm64-v8a")
}

dependencies {

}
